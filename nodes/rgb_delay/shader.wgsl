struct VertexOutput {
    @builtin(position) position: vec4<f32>,
    @location(0) uv: vec2<f32>,
}

@vertex
fn vs_main(@builtin(vertex_index) vid: u32) -> VertexOutput {
    var out: VertexOutput;
    let x = f32((vid << 1u) & 2u);
    let y = f32(vid & 2u);
    out.position = vec4<f32>(x * 2.0 - 1.0, 1.0 - y * 2.0, 0.0, 1.0);
    out.uv = vec2<f32>(x, y);
    return out;
}

struct Params {
    red_offset: f32,
    green_offset: f32,
    blue_offset: f32,
}

@group(0) @binding(0) var input_sampler: sampler;
@group(0) @binding(1) var input_texture: texture_2d<f32>;
@group(0) @binding(2) var<uniform> params: Params;

@fragment
fn fs_main(in: VertexOutput) -> @location(0) vec4<f32> {
    // Sample each channel at different horizontal positions
    let r = textureSample(input_texture, input_sampler, in.uv + vec2<f32>(params.red_offset, 0.0)).r;
    let g = textureSample(input_texture, input_sampler, in.uv + vec2<f32>(params.green_offset, 0.0)).g;
    let b = textureSample(input_texture, input_sampler, in.uv + vec2<f32>(params.blue_offset, 0.0)).b;
    let a = textureSample(input_texture, input_sampler, in.uv).a;

    return vec4<f32>(r, g, b, a);
}